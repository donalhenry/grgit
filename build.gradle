plugins {
  id 'org.ajoberstar.defaults' version '0.8.2'
  id 'org.jbake.site' version '1.0.0'
  id 'java-gradle-plugin'
  id 'groovy'
}

group = 'org.ajoberstar'
description = 'The Groovy way to use Git.'

sourceCompatibility = '1.8'

publishing {
	// publications {
	// 	mavenStuff(MavenPublication) {
	// 		from components.java
	// 	}
	// }
	repositories {
		maven {
			url "https://artifactory.scm.awsbest.turner.com/artifactory/libs-snapshot-local"
			credentials {
				username "bpsadmin"
				password "nit6#deg"
			}
		}
	}
}


sourceSets {
  compatTest {
    compileClasspath += main.output
    runtimeClasspath += main.output
  }
}

configurations.compatTestCompile.extendsFrom configurations.compile

jbake {
  srcDirName = 'docs'
}

gitPublish {
  contents {
    from "${buildDir}/${jbake.destDirName}"
  }
}

gitPublishCopy.dependsOn bake

repositories {
  mavenCentral()
}

def jgitVersion = '4.8.0.201706111038-r'

dependencies {
  // groovy
  compileOnly 'org.codehaus.groovy:groovy-all:2.4.12'

  // gradle
  compileOnly gradleApi()

  // jgit
  compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
  compile "org.eclipse.jgit:org.eclipse.jgit.ui:$jgitVersion"

  // agentproxy
  ['jsch', 'pageant', 'sshagent', 'usocket-jna', 'usocket-nc'].each {
    compile "com.jcraft:jsch.agentproxy.${it}:0.0.9"
  }
  // agentproxy pulls this in, but we need to force a newer version
  compile 'com.jcraft:jsch:0.1.54'

  // logging
  compile 'org.slf4j:slf4j-api:1.7.25'
  testRuntime 'org.slf4j:slf4j-simple:1.7.25'

  // testing
  testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
  compatTestCompile('org.spockframework:spock-core:1.1-groovy-2.4')
}

test {
  useJUnit {
    excludeCategories 'org.ajoberstar.grgit.fixtures.PlatformSpecific'
  }
}

['Windows', 'Linux', 'Mac'].each { platform ->
  tasks.create("test${platform}", Test) {
    useJUnit {
      includeCategories "org.ajoberstar.grgit.fixtures.${platform}Specific"
    }
    System.properties.each { key, value ->
      if (key.startsWith('org.ajoberstar.grgit.test')) {
        systemProperties((key): value)
      }
    }
  }
}

stutter {
    supports '3.0', '3.1', '3.2', '3.2.1', '3.3', '3.4', '3.5', '3.5.1', '4.0', '4.0.1', '4.0.2', '4.1-rc-1'
}

model {
  bintray {
    owner = 'ajoberstar'
    repo = 'maven'
    pkg = 'grgit'
  }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/grgit'
    vcsUrl = 'https://github.com/ajoberstar/grgit.git'
    description = 'The Groovy way to use Git'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.grgit'
            displayName = 'The Groovy way to use Git'
            tags = ['git', 'groovy']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

wrapper {
  gradleVersion = '3.5.1'
}
